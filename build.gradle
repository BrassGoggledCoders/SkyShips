buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
        maven {
            name = "Parchment"
            url = 'https://maven.parchmentmc.org'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.parchmentmc', name: 'librarian', version: '1.+'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'maven-publish'

apply from: 'https://raw.githubusercontent.com/SizableShrimp/Forge-Class-Remapper/main/classremapper.gradle'

version = '1.0.0'
group = 'xyz.brassgoggledcoders.skyships' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'skyships'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'parchment', version: '2021.12.19-1.18.1'

    runs {
        client {
            workingDirectory project.file('run/client')

            property 'forge.logging.console.level', 'debug'

            mods {
                skyships {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')

            property 'forge.logging.console.level', 'debug'

            mods {
                skyships {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run/data')

            property 'forge.logging.console.level', 'debug'

            args '--mod', 'skyships', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                skyships {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    shade
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url "https://maven.tterrag.com/"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.1-39.0.59'

    def registrate = "com.tterrag.registrate:Registrate:MC1.18-1.0.21"
    implementation fg.deobf(registrate)
    shade registrate
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'com.tterrag.registrate', 'xyz.brassgoggledcoders.skyships.repack.registrate'
}


jar {
    manifest {
        attributes([
                "Specification-Title"     : "skyships",
                "Specification-Vendor"    : "Brass Goggled Coders",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Brass Goggled Coders",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
